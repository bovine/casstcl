# Commands covered:  casstcl::cass
#
# This file contains a collection of tests for one or more of the Tcl
# package commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Written by Joe Mistachkin.
#
# See the file "license.terms" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.

if {[lsearch [namespace children] ::tcltest] == -1} then {
  package require tcltest
  namespace import ::tcltest::*
}

set path [file normalize [file dirname [info script]]]
package require casstcl

###############################################################################

if {[llength [info commands appendArgs]] == 0} then {
  proc appendArgs { args } {
    set result ""; eval append result $args
  }
}

###############################################################################

if {[llength [info commands getEnvVar]] == 0} then {
  proc getEnvVar { name {default ""} } {
    if {![info exists ::env($name)]} then {
      if {![info exists ::warn($name)]} then {
        tcltest::Warn [appendArgs \
            "missing environment variable \"" $name \
            "\", using default \"" $default \"...]

        set ::warn($name) 1
      }

      return $default
    }
    return $::env($name)
  }
}

###############################################################################

if {[llength [info commands getDictValue]] == 0} then {
  proc getDictValue { dictionary name {default ""} {wrap ""} } {
    if {[llength [info commands dict]] > 0} then {
      if {[dict exists $dictionary $name]} then {
        return [appendArgs $wrap [dict get $dictionary $name] $wrap]
      }

      return $default
    } else {
      foreach {pairName pairValue} $dictionary {
        if {$pairName eq $name} then {
          return [appendArgs $wrap $pairValue $wrap]
        }
      }

      return $default
    }
  }
}

###############################################################################

if {[llength [info commands cass_test_cleanup_server]] == 0} then {
  proc cass_test_cleanup_server {} {
    cass_test_connect cmd

    foreach keyspace [$cmd keyspaces] {
      if {[string match cass_test_safe_to_delete_* $keyspace]} then {
        if {[catch {
          $cmd exec [subst $::cass_test_cql(drop)]
        } error] == 0} then {
          cass_test_output [appendArgs \
              "success dropping keyspace \"" $keyspace \"]
        } else {
          cass_test_output [appendArgs \
              "failure dropping keyspace \"" $keyspace "\": " \
              $error]
        }
      }
    }

    cass_test_service_events svc
    cass_test_cleanup_session cmd
  }
}

###############################################################################

if {[llength [info commands cass_test_output]] == 0} then {
  proc cass_test_output { string } {
    catch {
      puts [tcltest::outputChannel] [appendArgs "---- " $string]
      flush [tcltest::outputChannel]
    }
  }
}

###############################################################################

if {[llength [info commands cass_test_logging_callback]] == 0} then {
  proc cass_test_logging_callback { dictionary } {
    cass_test_output $dictionary
  }
}

###############################################################################

if {[llength [info commands cass_test_future_callback]] == 0} then {
  proc cass_test_future_callback { varName microseconds future } {
    upvar 1 $varName result

    lappend result [$future isready]

    if {$microseconds < 0} then {
      lappend result [$future wait]
    } else {
      lappend result [$future wait $microseconds]
    }

    lappend result [$future status]
    lappend result [$future error_message]

    set rows [list]

    $future foreach row {
      lappend rows [array get row]
    }

    lappend result $rows
  }
}

###############################################################################

if {[llength [info commands cass_test_connect]] == 0} then {
  proc cass_test_connect {
          varName {cmdName ""} {points ""} {port ""} {timeout ""} } {
    upvar 1 $varName newCmdName

    set newCmdName [casstcl::cass create \
        [expr {[string length $cmdName] > 0 ? $cmdName : "#auto"}]]

    if {[string length $points] == 0} then {
      set points [getEnvVar \
          CASSTCL_CONTACT_POINTS 127.0.0.1,127.0.0.2,127.0.0.3]
    }

    if {[string length $port] == 0} then {
      set port [getEnvVar CASSTCL_PORT 9042]
    }

    if {[string length $timeout] == 0} then {
      set timeout $::cass_test_timeout
    }

    $newCmdName contact_points $points
    $newCmdName port $port
    $newCmdName connect_timeout $timeout
    $newCmdName connect
  }
}

###############################################################################

if {[llength [info commands cass_test_service_events]] == 0} then {
  proc cass_test_service_events { varName {milliseconds ""} } {
    if {[string length $milliseconds] == 0} then {
      set milliseconds $::cass_test_timeout
    }
    cass_test_output "starting to service events"
    uplevel 1 [list unset -nocomplain $varName]
    uplevel 1 [list after $milliseconds [list set $varName 1]]
    uplevel 1 [list vwait $varName]
    cass_test_output "done with servicing events"
  }
}

###############################################################################

if {[llength [info commands cass_test_cleanup_session]] == 0} then {
  proc cass_test_cleanup_session { varName {delete true} {drop false} } {
    upvar 1 $varName cmdName

    #
    # TODO: Why does trying to DROP a nonexistent keyspace cause
    #       tests to hang?
    #
    if {$drop} then {
      set keyspace [cass_test_get_keyspace]

      catch {
        $cmdName exec [subst $::cass_test_cql(drop)]
      }
    }

    cass_test_cleanup_object cmdName $delete
  }
}

###############################################################################

if {[llength [info commands cass_test_cleanup_object]] == 0} then {
  proc cass_test_cleanup_object { varName {delete true} } {
    upvar 1 $varName cmdName

    #
    # NOTE: When the delete argument is non-zero, call the method;
    #       otherwise, rely on the Tcl command deletion callback.
    #
    if {$delete} then {
      catch {$cmdName delete}
    } else {
      rename $cmdName ""
    }
  }
}

###############################################################################

if {[llength [info commands cass_test_get_keyspace]] == 0} then {
  proc cass_test_get_keyspace { {suffix ""} } {
    set result [appendArgs cass_test_safe_to_delete_ [pid]]

    if {[string length $suffix] > 0} then {
      append result _ $suffix
    }

    return $result
  }
}

###############################################################################

if {![info exists cass_test_log_level]} then {
  set cass_test_log_level [getEnvVar CASSTCL_LOG_LEVEL \
      [expr {[tcltest::IsVerbose skip] ? "trace" : "disabled"}]]
}

###############################################################################

if {![info exists cass_test_timeout]} then {
  set cass_test_timeout [getEnvVar CASSTCL_TIMEOUT 5000]; # milliseconds
}

###############################################################################

if {![info exists cass_test_cql]} then {
  set cass_test_cql(0) {
    CREATE KEYSPACE $keyspace WITH REPLICATION = {
      'class' : 'SimpleStrategy', 'replication_factor' : 1
    };
  }

  set cass_test_cql(1) {
    SELECT keyspace_name FROM system.schema_keyspaces;
  }

  set cass_test_cql(2) {
    SELECT keyspace_name FROM system.schema_keyspaces
    WHERE keyspace_name = '$keyspace';
  }

  set cass_test_cql(3) {
    CREATE TABLE $keyspace.main (x text PRIMARY KEY);
  }

  set cass_test_cql(4) {
    INSERT INTO $keyspace.main (x) VALUES ('[string map [list ' ''] $value]');
  }

  set cass_test_cql(5) {
    INSERT INTO $keyspace.main (x) VALUES (?);
  }

  set cass_test_cql(drop) {
    DROP KEYSPACE IF EXISTS $keyspace;
  }
}

###############################################################################

test cass-1.1 {overall command usage} -body {
  list [catch {casstcl::cass} errMsg] $errMsg
} -cleanup {
  unset -nocomplain errMsg
} -result {1 {wrong # args: should be "casstcl::cass subcommand ?args?"}}

###############################################################################

test cass-2.1 {create sub-command usage} -body {
  list [catch {casstcl::cass create} errMsg] $errMsg
} -cleanup {
  unset -nocomplain errMsg
} -result {1 {wrong # args: should be "casstcl::cass option arg"}}

###############################################################################

test cass-2.2 {logging_callback sub-command usage} -body {
  list [catch {casstcl::cass logging_callback} errMsg] $errMsg
} -cleanup {
  unset -nocomplain errMsg
} -result {1 {wrong # args: should be "casstcl::cass option arg"}}

###############################################################################

test cass-3.1 {set and reset logging callback} -body {
  list [catch {
    casstcl::cass logging_callback cass_test_logging_callback
    casstcl::cass log_level $cass_test_log_level

    #
    # TODO: Permit the logging callback to be unset/reset.  This would
    #       actually require the DataStax driver itself to be modified
    #       as it does not currently support getting (for save/restore
    #       semantics) or resetting/unsetting the logging callback in
    #       any way.  Also, by default, it outputs to stderr, which
    #       causes tcltest to believe that this test file has failed.
    #
    # casstcl::cass logging_callback ""
  } errMsg] $errMsg
} -cleanup {
  unset -nocomplain errMsg
} -result {0 {}}

###############################################################################

test cass-4.1 {session with manual name} -body {
  list [catch {
    set cmd cass-4.1
    casstcl::cass create $cmd
    rename $cmd ""
  } errMsg] $errMsg
} -cleanup {
  unset -nocomplain cmd errMsg
} -result {0 {}}

###############################################################################

test cass-4.2 {session with automatic name} -body {
  list [catch {
    set cmd [casstcl::cass create #auto]
    rename $cmd ""
  } errMsg] $errMsg
} -cleanup {
  unset -nocomplain cmd errMsg
} -result {0 {}}

###############################################################################

test cass-5.1 {connect (failure)} -body {
  list [catch {
    cass_test_connect cmd "" "" 11111; # NOTE: Port must be "invalid".
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain svc cmd errMsg
} -result {1 {cassandra error: No hosts available}}

###############################################################################

test cass-5.2 {connect (success)} -body {
  list [catch {
    cass_test_connect cmd
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain svc cmd errMsg
} -result {0 {}}

###############################################################################

test cass-6.1 {metadata: list keyspaces} -body {
  list [catch {
    cass_test_connect cmd
    $cmd keyspaces
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain svc cmd errMsg
} -match regexp -result {^0 \{.+\}$}

###############################################################################

test cass-6.2 {metadata: list tables} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass62 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd tables $keyspace
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {0 cass62}

###############################################################################

test cass-6.3 {metadata: list tables with bad keyspace} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass63 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd tables cass_test_bad
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {1 {keyspace 'cass_test_bad' not found}}

###############################################################################

test cass-6.4 {metadata: list columns} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass64 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns $keyspace cass64
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {0 column_one}

###############################################################################

test cass-6.5 {metadata: list columns with bad keyspace} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass65 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns cass_test_bad cass65
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {1 {keyspace 'cass_test_bad' not found}}

###############################################################################

test cass-6.6 {metadata: list columns with bad table} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass66 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns $keyspace cass66_bad
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result [subst {1\
{table 'cass66_bad' not found in keyspace '[cass_test_get_keyspace]'}}]

###############################################################################

test cass-6.7 {metadata: list columns types} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass67 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns_with_types $keyspace cass67
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {0 {column_one text}}

###############################################################################

test cass-6.8 {metadata: list columns types with bad keyspace} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass68 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns_with_types cass_test_bad cass68
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result {1 {keyspace 'cass_test_bad' not found}}

###############################################################################

test cass-6.9 {metadata: list columns types with bad table} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass69 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd columns_with_types $keyspace cass69_bad
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc cmd errMsg
} -result [subst {1\
{table 'cass69_bad' not found in keyspace '[cass_test_get_keyspace]'}}]

###############################################################################

test cass-7.1 {close without delete} -body {
  list [catch {
    cass_test_connect cmd
    $cmd close
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd false

  unset -nocomplain svc cmd errMsg
} -result {0 {}}

###############################################################################

test cass-7.2 {close with delete} -body {
  list [catch {
    cass_test_connect cmd
    $cmd close
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true

  unset -nocomplain svc cmd errMsg
} -result {0 {}}

###############################################################################

test cass-8.1 {select without page size} -body {
  list [catch {
    cass_test_connect cmd

    set result [list]

    $cmd select $cass_test_cql(1) row {
      lappend result [array get row]
    }

    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain result row svc cmd errMsg
} -match regexp -result \
{^0 \{(?:\{keyspace_name (?:.*)\}) (?:\{keyspace_name (?:.*)\})*\}$}

###############################################################################

test cass-8.2 {select with page size} -body {
  list [catch {
    cass_test_connect cmd

    set result [list]

    $cmd select -pagesize 1 $cass_test_cql(1) row {
      lappend result [array get row]
    }

    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain result row svc cmd errMsg
} -match regexp -result \
{^0 \{(?:\{keyspace_name (?:.*)\}) (?:\{keyspace_name (?:.*)\})*\}$}

###############################################################################

test cass-9.1 {exec no batch without callback (synchronous)} -body {
  list [catch {
    cass_test_connect cmd
    $cmd exec $cass_test_cql(1)
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain svc cmd errMsg
} -result {0 {}}

###############################################################################

test cass-9.2 {exec no batch with callback (asynchronous)} -body {
  list [catch {
    cass_test_connect cmd
    set result [list]
    $cmd exec \
        -callback [list cass_test_future_callback result -1] \
        $cass_test_cql(1)
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain result svc cmd errMsg
} -match regexp -result {^0 \{1 \{\} CASS_OK \{\} \{(?:\{keyspace_name\
(?:.*)\}) (?:\{keyspace_name (?:.*)\})*\}\}$}

###############################################################################

test cass-9.3 {exec with batch without callback (synchronous)} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    set batch [$cmd batch #auto]
    set value 1; $batch add [subst $cass_test_cql(4)]
    $cmd exec -batch $batch
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc value batch cmd errMsg
} -result {0 {}}

###############################################################################

test cass-9.4 {exec with batch with callback (asynchronous)} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    set batch [$cmd batch #auto]
    set value 1; $batch add [subst $cass_test_cql(4)]
    set result [list]
    $cmd exec -batch $batch \
        -callback [list cass_test_future_callback result -1]
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain result keyspace svc value batch cmd errMsg
} -result {0 {1 {} CASS_OK {} {}}}

###############################################################################

test cass-9.5 {exec no batch without callback using -array} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    $cmd reimport_column_type_map
    array set row [list x 1]
    $cmd exec \
        -table [appendArgs $keyspace .main] \
        -array row [subst $cass_test_cql(5)] x
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain row svc cmd errMsg
} -result {0 {}}

###############################################################################

test cass-9.6 {exec no batch with callback using -array} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    $cmd reimport_column_type_map
    set result [list]
    array set row [list x 1]
    $cmd exec \
        -callback [list cass_test_future_callback result -1] \
        -table [appendArgs $keyspace .main] \
        -array row [subst $cass_test_cql(5)] x
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain result row svc cmd errMsg
} -result {0 {1 {} CASS_OK {} {}}}

###############################################################################

test cass-9.7 {exec no batch without callback using -prepared no args} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    $cmd reimport_column_type_map
    set value 1
    set prepared [$cmd prepare #auto \
        [appendArgs $keyspace .main] \
        [subst $cass_test_cql(4)]]
    $cmd exec -prepared $prepared
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain svc value cmd errMsg
} -result {0 {}}

###############################################################################

test cass-9.8 {exec no batch with callback using -prepared no args} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    $cmd reimport_column_type_map
    set value 1
    set prepared [$cmd prepare #auto \
        [appendArgs $keyspace .main] \
        [subst $cass_test_cql(4)]]
    set result [list]
    $cmd exec \
        -callback [list cass_test_future_callback result -1] \
        -prepared $prepared 1 2
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain result svc value cmd errMsg
} -result {0 {1 {} CASS_OK {} {}}}

###############################################################################

test cass-10.1 {async without callback} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace cass101_async]
    cass_test_connect cmd
    $cmd async [subst $cass_test_cql(0)]
    cass_test_service_events svc
    $cmd select [subst $cass_test_cql(2)] row {
      lappend result [array get row]
    }
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd true true

  unset -nocomplain result row keyspace svc cmd errMsg
} -match regexp -result {^0 \{\{keyspace_name .*\}\}$}

###############################################################################

test cass-10.2 {async with callback} -body {
  list [catch {
    cass_test_connect cmd
    set result [list]
    $cmd exec \
        -callback [list cass_test_future_callback result -1] \
        $cass_test_cql(1)
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_session cmd

  unset -nocomplain result svc cmd errMsg
} -match regexp -result {^0 \{1 \{\} CASS_OK \{\} \{(?:\{keyspace_name\
(?:.*)\}) (?:\{keyspace_name (?:.*)\})*\}\}$}

###############################################################################

test cass-10.3 {async with batch without callback (synchronous)} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    set batch [$cmd batch #auto]
    set value 1; $batch add [subst $cass_test_cql(4)]
    set future [$cmd async -batch $batch]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain keyspace svc future value batch cmd errMsg
} -match regexp -result {^0 future\d+$}

###############################################################################

test cass-10.4 {async with batch with callback (asynchronous)} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst $cass_test_cql(3)]
    set batch [$cmd batch #auto]
    set value 1; $batch add [subst $cass_test_cql(4)]
    set result [list]
    $cmd async -batch $batch \
        -callback [list cass_test_future_callback result -1]
    cass_test_service_events svc
    set result
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain result keyspace svc value batch cmd errMsg
} -result {0 {1 {} CASS_OK {} {}}}

###############################################################################

test cass-11.1 {batch with manual name and type} -body {
  list [catch {
    cass_test_connect cmd
    set result [list]
    set batch cass-11.1
    $cmd batch $batch unlogged
    lappend result [$batch count]
    lappend result [$batch add $cass_test_cql(1)]
    lappend result [$batch count]
    lappend result [$batch reset]
    lappend result [$batch count]
    lappend result [$batch add $cass_test_cql(1)]
    lappend result [$batch count]
    lappend result [$batch consistency]
    lappend result [$batch consistency quorum]
    lappend result [$batch consistency]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd

  unset -nocomplain result svc batch cmd errMsg
} -result {0 {0 {} 1 {} 0 {} 1 one {} quorum}}

###############################################################################

test cass-11.2 {batch with automatic name and type} -body {
  list [catch {
    cass_test_connect cmd
    set result [list]
    set batch [$cmd batch #auto]
    lappend result [$batch count]
    lappend result [$batch add $cass_test_cql(1)]
    lappend result [$batch count]
    lappend result [$batch reset]
    lappend result [$batch count]
    lappend result [$batch add $cass_test_cql(1)]
    lappend result [$batch count]
    lappend result [$batch consistency]
    lappend result [$batch consistency quorum]
    lappend result [$batch consistency]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd

  unset -nocomplain result svc batch cmd errMsg
} -result {0 {0 {} 1 {} 0 {} 1 one {} quorum}}

###############################################################################

test cass-11.3 {batch upsert} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass113 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd reimport_column_type_map
    set result [list]
    set batch [$cmd batch #auto]
    lappend result [$batch count]
    lappend result [$batch upsert \
        [appendArgs $keyspace .cass113] [list column_one test1]]
    lappend result [$batch count]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain result keyspace svc batch cmd errMsg
} -result {0 {0 {} 1}}

###############################################################################

test cass-12.1 {prepare with manual name} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass121 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd reimport_column_type_map
    set prepared cass-12.1
    $cmd prepare $prepared \
        [appendArgs $keyspace. cass121] [subst {
      SELECT * FROM $keyspace.cass121;
    }]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain result keyspace svc prepared batch cmd errMsg
} -result {0 cass-12.1}

###############################################################################

test cass-12.2 {prepare with automatic name} -body {
  list [catch {
    set keyspace [cass_test_get_keyspace]
    cass_test_connect cmd
    $cmd exec [subst $cass_test_cql(0)]
    $cmd exec [subst {
      CREATE TABLE $keyspace.cass122 (
        column_one text,
        PRIMARY KEY(column_one)
      );
    }]
    $cmd reimport_column_type_map
    set prepared [$cmd prepare #auto \
        [appendArgs $keyspace. cass122] [subst {
      SELECT * FROM $keyspace.cass122;
    }]]
  } errMsg] $errMsg
} -cleanup {
  cass_test_service_events svc
  cass_test_cleanup_object batch
  cass_test_cleanup_session cmd true true

  unset -nocomplain result keyspace svc batch cmd errMsg
} -match regexp -result {^0 prepared\d+$}

###############################################################################

#
# NOTE: Enable this block to DROP all test keyspaces created by these tests.
#
if {0} then {
  cass_test_cleanup_server
}

###############################################################################

foreach procName [concat [info commands cass_test_*] \
    [list appendArgs getEnvVar getDictValue]] {
  rename $procName ""
}

unset -nocomplain procName

###############################################################################

foreach varName [info vars cass_test_*] {
  unset -nocomplain $varName
}

unset -nocomplain varName

###############################################################################

unset -nocomplain warn path

# cleanup
::tcltest::cleanupTests
return
